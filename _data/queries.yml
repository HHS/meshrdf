#important - see _config.yml query-types for the list of allowable query types.

- name: Retrieve a list of predicates in MeSH RDF
  notes: This query gives a complete list of MeSH RDF predicates
  type: Getting Started
  string: |+
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   
   SELECT DISTINCT ?p
   FROM <http://id.nlm.nih.gov/mesh2014>
   WHERE {
   
     ?s ?p ?o
     
   }
   ORDER BY ?p

- name: Retrieve all meshv classes in MeSH RDF
  notes: This query gives a unique list of all MeSH classes
  type: Getting Started
  string: |+
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
   
   SELECT DISTINCT ?class
   FROM <http://id.nlm.nih.gov/mesh2014>
   WHERE {
   
      ?s rdf:type ?class
      
    }
    ORDER BY ?class
    
- name: Find triples with 'Ofloxacin' as the object
  notes: A basic subject, predicate, object triple in MeSH RDF.
  type: Getting Started
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
     ?s ?p mesh:D015242 .
   }

- name:  Find the MeSH headings and labels of Pharmacologic Actions for 'Ofloxacin'
  notes: Show the MeSH headings and labels of Pharmacologic Actions for Ofloxacin.
  type: Getting Started
  string: |+
   
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT *
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
     mesh:D015242 meshv:pharmacologicalAction ?pa .
     ?pa rdfs:label ?paLabel
   }
   
- name: Descriptors and SCRs that have the Pharmacologic Action 'Anti-Bacterial Agents'
  notes: A filter query of MeSH predicates based on a certain type of Pharmacologic Action
  type: Predicate
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT ?d ?dLabel
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
    ?d meshv:pharmacologicalAction ?pa.
    ?d rdfs:label ?dLabel .
    ?pa rdfs:label "Anti-Bacterial Agents"
   }
   
   ORDER BY ASC (?dLabel)
   
- name: Find all MeSH descriptors with an allowable qualifier of 'adverse effects'
  notes: MeSH has rules that govern which qualifiers can be used with which MeSH Headings.
  type: Descriptors and Qualifiers
  string: |+
     PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
     PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
     PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
     
     SELECT distinct ?d ?dLabel  
     FROM  <http://id.nlm.nih.gov/mesh2014>
     
     WHERE {  
        ?d meshv:allowableQualifier ?q .  
        ?q rdfs:label "adverse effects" .  
        ?d rdfs:label ?dLabel  
     }
     
- name: Find all MeSH descriptors that are ancestors of MeSH descriptor D005138 'Eyebrow'
  notes: The MeSH heading for 'Eybrow' can be retrieved through various paths in the MeSH tree.  This query demonstrates how to 'explode up' the MeSH tree to find all ancestors.
  type: Transitive
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   SELECT ?treeNode ?ancestorTreeNode ?ancestor ?alabel
   FROM <http://id.nlm.nih.gov/mesh2014>
   
   WHERE {
      mesh:D005138 meshv:treeNumber ?treeNode .
      ?treeNode meshv:broaderTransitive+ ?ancestorTreeNode .
      ?ancestor meshv:treeNumber ?ancestorTreeNode .
      ?ancestor rdfs:label ?alabel
   }
   
   ORDER BY ?treeNode ?ancestorTreeNode
   
- name: Find all MeSH descriptors that are descendants of MeSH descriptor D009369 'Neoplasm'
  notes: Find all descendants (not just immediate children) of 'Neoplasm'.  This query 'explodes down' the MeSH tree.
  type: Transitive
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
    SELECT DISTINCT ?desc ?desc_label
    FROM <http://id.nlm.nih.gov/mesh2014>
   
    WHERE {
      mesh:D009369 meshv:treeNumber ?tn .
      ?desctn meshv:broaderTransitive+ ?tn .
      ?desc meshv:treeNumber ?desctn .
      ?desc rdfs:label ?desc_label .
    }
    
    ORDER BY ?desc_label

- name: Retrieve all MeSH descriptors or concepts with 'infection' anywhere in its name
  notes: Find MeSH descriptors or sub-concepts based on string.
  type: String
  string: |+
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
    SELECT ?d ?dName ?c ?cName
    FROM <http://id.nlm.nih.gov/mesh2014>
    
    WHERE {
      
    ?d a meshv:Descriptor .
    ?d meshv:concept ?c .
    ?d rdfs:label ?dName .
    ?c rdfs:label ?cName
    FILTER(REGEX(?dName,"infection","i") || REGEX(?cName,"infection","i"))
    
    }
    ORDER BY ?d
  
- name: Find all MeSH Descriptors created for 2014 MeSH that are treed under diseases.
  notes: This query leverages the MeSH 'C' tree to find new diseases in MeSH.
  type: Aggregation
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
   
    SELECT DISTINCT ?d ?name
    FROM <http://id.nlm.nih.gov/mesh2014>
    
    WHERE {
    
    ?d a meshv:Descriptor .
    ?d rdfs:label ?name .
    ?d meshv:treeNumber ?tn .
    ?d meshv:dateEstablished ?dateEstablished
    FILTER(?dateEstablished = xsd::date("2014-01-01"))
    FILTER(REGEX(?tn,"C"))
   
    }
    
    ORDER BY ?name
    
- name: Retrieve all the MeSH concepts and semantic types associated with the MeSH descriptor D000647 'Amnesia'
  notes: MeSH Descriptors are collections of MeSH concepts.  The name of the preferred concept (prefCon) always contributes the name of the Descriptor.
  type: Descriptors and Concepts
  string: |+
   
   PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
   PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
   PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
   
   CONSTRUCT {
    
    mesh:D000647 meshv:preferredConcept ?prefcon .
    ?prefcon ?p ?o .
    ?prefcon meshv:semanticType $semtype .
    ?prefcon meshv:narrower $subcon .
    $subcon rdfs:label $subconLabel .
    $semtype ?stp $sto .
    }
    
    FROM <http://id.nlm.nih.gov/mesh2014>
    
    WHERE {
    mesh:D000647 meshv:preferredConcept ?prefcon .
    ?prefcon ?p ?o .
    ?prefcon meshv:semanticType $semtype .
    ?prefcon meshv:narrower $subcon .
    $subcon rdfs:label $subconLabel .
    $semtype ?stp $sto .
    }

- name: Retrieve a list of all Semantic Types in MeSH
  notes: This is a distinct list of all Semantic Types (TUI and label) that have been assigned to MeSH concepts.
  type: Semantic Types
  string: |+
    PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
    PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    
    SELECT DISTINCT ?sty ?name
    FROM <http://id.nlm.nih.gov/mesh2014>
    WHERE {
     ?sty a meshv:SemanticType .
     ?sty rdfs:label ?name
     }
     ORDER BY ?sty
    
